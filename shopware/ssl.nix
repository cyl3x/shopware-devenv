{ config, lib, pkgs, ... }: let
  cfg = config.shopware.ssl;
in with lib; {
  options.shopware.ssl = {
    standalone = {
      enable = mkOption {
        description = ''
        Enable standalone ssl. Shopware will be available via https by default. A fallback port can be defined for http.
        Self signed certificates will generated by caddy.
        '';
        type = types.bool;
        default = false;
      };
      fallbackPort = mkOption {
        description = "Fallback port for http";
        type = types.nullOr types.port;
        default = config.shopware.port + 80;
      };
    };

    proxy = {
      enable = mkOption {
        description = ''
        Enable ssl via proxy. Shopware will be available via https by default. Used domain will taken from `shopware.domain`.
       
        No other webserver configuration will be done, it's assumed that a second devenv instance is running as proxy. See `shopware-proxy.enable` for more information.
        '';
        type = types.bool;
        default = false;
      };
      port = mkOption {
        description = "Port on which the proxy runs.";
        type = types.nullOr types.port;
        default = 2000;
      };
      devenv = mkOption {
        description = "Path to the devenv instance that serves as proxy.";
        type = types.str;
        default = "${config.env.DEVENV_ROOT}/../.devenv";
      };
      rootCA = mkOption {
        description = "Path to the root certificate of the proxy.";
        readOnly = true;
        type = types.str;
        default = "${cfg.proxy.devenv}/state/caddy/data/caddy/pki/authorities/local/root.crt";
      };
    };

    enableCerts = mkOption {
      description = "Enable creation of a trusted root cert for node.js and php.";
      type = types.bool;
      default = cfg.standalone.enable || cfg.proxy.enable;
    };
  };

  config = mkIf cfg.enableCerts (let
    rootCA = if cfg.standalone.enable
      then "${config.env.DEVENV_STATE}/caddy/data/caddy/pki/authorities/local/root.crt"
      else if cfg.proxy.enable
      then cfg.proxy.rootCA
      else null;
    system = if pkgs.stdenv.isLinux then "/etc/ssl/certs/ca-certificates.crt" else "/etc/ssl/cert.pem";
    combined = "${config.env.DEVENV_STATE}/ca-certificates.crt";
  in {
    env.NODE_EXTRA_CA_CERTS = mkDefault combined;

    languages.php.ini = ''
      openssl.cafile = ${combined}
    '';

    process.manager.before = ''
      cat ${escapeShellArg system} ${escapeShellArg rootCA} > ${escapeShellArg combined}
    '';
  });
}
